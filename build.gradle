buildscript {
	repositories {
		mavenLocal()
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
	}
}

plugins {
    id 'signing'
}

group = 'com.bertramlabs.plugins'

ext {
    isBuildSnapshot = version.endsWith('-SNAPSHOT')
    isReleaseVersion = !isBuildSnapshot
}



apply plugin: 'groovy'
apply plugin: 'idea'

if (isReleaseVersion) {
    apply plugin: "io.github.gradle-nexus.publish-plugin"
    nexusPublishing {
        repositories {
            sonatype {
                if(project.hasProperty('mavenUser')) {
                    username = mavenUser
                    password = mavenPassword
                }   
            }
        }
    }
} else {
    publishing {
        repositories {
            maven {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}

subprojects { project ->
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    afterEvaluate {
        signing {
            required { isReleaseVersion && gradle.taskGraph.hasTask("publish") }
            sign publishing.publications.maven
        }
    }

    tasks.withType(Sign) {
        onlyIf { isReleaseVersion }
    }

    //do not generate extra load on Nexus with new staging repository if signing fails
    tasks.withType(io.github.gradlenexus.publishplugin.InitializeNexusStagingRepository).configureEach {
        shouldRunAfter(tasks.withType(Sign))
    }
    
}



repositories {
	mavenCentral()
}



test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}
